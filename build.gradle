plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'io.freefair.lombok' version '6.3.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://jitpack.io/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url = 'https://repo.maven.apache.org/maven2/' }
    maven { url = 'https://repo.viaversion.com' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
}

dependencies {
    implementation 'io.github.retrooper:packetevents:362d021745'
    implementation 'com.github.steveice10:packetlib:2.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'club.minnced:discord-webhooks:0.7.4'
    implementation 'it.unimi.dsi:fastutil:8.5.6'

    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'com.viaversion:viaversion-api:4.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.16.0'
    compileOnly 'net.essentialsx:EssentialsX:2.19.0'
}

group = 'ac.grim.grimac'
version = '0.0.3'
description = 'GrimAC'
java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    minimize()
    relocate 'io.github.retrooper.packetevents', 'ac.grim.grimac.shaded.packetevents'
    relocate 'com.github.steveice10.packetlib', 'ac.grim.grimac.shaded.packetlib'
    relocate 'co.aikar.commands', 'ac.grim.grimac.shaded.acf'
    relocate 'co.aikar.locales', 'ac.grim.grimac.shaded.locales'
    relocate 'org.bstats', 'ac.grim.grimac.shaded.bstats'
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

build.dependsOn shadowJar