plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

repositories {
    mavenLocal()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } // Bstats, Spigot
    maven { url = 'https://jitpack.io/' } // Grim API
    maven { url = 'https://repo.viaversion.com' } // ViaVersion
    maven { url = "https://repo.aikar.co/content/groups/aikar/" } // ACF
    maven { url = "https://nexus.scarsz.me/content/groups/public" } // Configuralize
    maven { url = "https://clojars.org/repo" } // MultiPaper MultiLib
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" } // Floodgate
    maven {
        name = 'codemc-snapshots'
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    } // PacketEvents
    mavenCentral() // FastUtil, Discord-Webhooks, Lombok
}

dependencies {
    implementation 'com.github.retrooper.packetevents:spigot:2.0-SNAPSHOT'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'club.minnced:discord-webhooks:0.8.0'
    implementation 'it.unimi.dsi:fastutil:8.5.9'
    implementation 'org.jetbrains:annotations:23.0.0' // Why is this needed to compile?
    implementation 'github.scarsz:configuralize:1.4.0'
    implementation 'com.github.puregero:multilib:1.1.3'

    implementation 'com.github.MWHunter:GrimAPI:add576ba8b'
    //used for local testing: implementation 'ac.grim.grimac:GrimAPI:1.0'

    compileOnly 'org.geysermc.floodgate:api:2.0-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'com.viaversion:viaversion-api:4.1.1'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'io.netty:netty-all:4.1.85.Final'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

group = 'ac.grim.grimac'
version = '2.3.31'
description = 'GrimAC'
java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

shadowJar {
    minimize()
    relocate 'io.github.retrooper.packetevents', 'ac.grim.grimac.shaded.io.github.retrooper.packetevents'
    relocate 'com.github.retrooper.packetevents', 'ac.grim.grimac.shaded.com.github.retrooper.packetevents'
    relocate 'co.aikar.acf', 'ac.grim.grimac.shaded.acf'
    relocate 'org.bstats', 'ac.grim.grimac.shaded.bstats'
    relocate 'club.minnced', 'ac.grim.grimac.shaded.discord-webhooks'
    relocate 'github.scarsz.configuralize', 'ac.grim.grimac.shaded.configuralize'
    relocate 'com.github.puregero', 'ac.grim.grimac.shaded.com.github.puregero'
    relocate 'com.google.gson', 'ac.grim.grimac.shaded.gson'
    relocate 'alexh', 'ac.grim.grimac.shaded.maps'
    relocate 'it.unimi.dsi.fastutil', 'ac.grim.grimac.shaded.fastutil'
    relocate 'net.kyori', 'ac.grim.grimac.shaded.kyori'
    relocate 'okhttp3', 'ac.grim.grimac.shaded.okhttp3'
    relocate 'okio', 'ac.grim.grimac.shaded.okio'
    relocate 'org.yaml.snakeyaml', 'ac.grim.grimac.shaded.snakeyaml'
    relocate 'org.slf4j', 'ac.grim.grimac.shaded.slf4j'
    relocate 'org.json', 'ac.grim.grimac.shaded.json'
    relocate 'org.intellij', 'ac.grim.grimac.shaded.intellij'
    relocate 'org.jetbrains', 'ac.grim.grimac.shaded.jetbrains'
    archiveFileName = "${project.name}-${project.version}.jar"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
